// vim: set expandtab sts=4 sw=4 ai:
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.1.0"
    }
}

apply plugin:"io.spring.dependency-management"
apply plugin: "org.openapi.generator"

description = "Candlepin Client Library"

// Don't run checkstyle on generated code
checkstyle.sourceSets = []

openApiGenerate {
    inputSpec = "${projectDir}/swagger.json"
    outputDir = "${buildDir}/generated"
    configFile = "${projectDir}/candlepin-api-config.json"
    generatorName = "java"
    // TODO get a valid spec
    validateSpec = false
    skipValidateSpec = true
    // TODO version = "$version"
}

sourceSets.main.java.srcDirs = ["${buildDir}/generated/src/main/java"]
compileJava.dependsOn tasks.openApiGenerate

ext.client = [
    spring_boot_version: "2.1.7.RELEASE",
    swagger_annotations_version: "1.5.21",
]

dependencyManagement {
    imports {
        // See https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-dependencies to
        // get listings of the contents of this BOM
        mavenBom "org.springframework.boot:spring-boot-dependencies:$client.spring_boot_version"
    }
    dependencies{
        dependency "io.swagger:swagger-annotations:$client.swagger_annotations_version"
//        dependency "org.codehaus.cargo:cargo-core-container-tomcat:1.7.4"
//        dependency "org.jboss.shrinkwrap:shrinkwrap-api:1.2.6"
    }
}

dependencies {
    implementation libraries.collections

    implementation "io.swagger:swagger-annotations:$client.swagger_annotations_version"
    implementation "org.springframework:spring-beans"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-test"
    implementation "org.springframework:spring-web"

    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
}
